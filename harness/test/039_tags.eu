{
  title: "039 tags"
  test-formats: [:yaml]
}

tagged-sym: :blah // { tag: "!Ref" }

`{ export: :suppress doc: "Heterogeneous array so can't be rendered to TOML" }
tagged-list: ["-", [:a, :b, :c]] // { tag: "!Fn::Join" }

homogeneous-tagged-list: [1, 2, 3, 4] // { tag: "!Periwabble" }

tagged-block: { a: 1 } // { tag: "!Blah" }

mblock: meta(tagged-block)

` { target: :test-metadata }
test-meta: {
  α: meta(tagged-sym).tag = "!Ref"
  β: meta(tagged-list).tag = "!Fn::Join"
  γ: meta(tagged-block).tag = "!Blah"
  RESULT: (α ∧ β ∧ γ) then(:PASS, :FAIL)
}

` { target: :test-render
  verify: [:renders-ref, :renders-join, :renders-periwabble, :renders-blah]}
test-render: {
  sym: tagged-sym
  lsa: tagged-list //<< { export: :display }
  lsb: homogeneous-tagged-list
  blk: tagged-block
}

renders-tag(tag, context): context.stdout filter(str.matches?(".*{tag}.*")) nil? then(:FAIL, :PASS)
renders-ref: renders-tag("!Ref")
renders-blah: renders-tag("!Blah")
renders-join: renders-tag("!Fn::Join")
renders-periwabble: renders-tag("!Periwabble")
